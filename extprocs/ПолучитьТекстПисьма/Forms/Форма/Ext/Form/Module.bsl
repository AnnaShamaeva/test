
&НаСервере
Процедура ПолучитьНаСервере()
	
	Попытка
		СодержаниеПисьма = СодержаниеСобытия(Письмо);	
	Исключение
		ТекстСообщения	= "Ошибка при получении содержания сообщения: " + ОписаниеОшибки();
		Сообщить(ТекстСообщения);
		Возврат;
	КонецПопытки;               
	
	ИсходноеТелоПисьма	= "";
	
	Если ЗначениеЗаполнено(СодержаниеПисьма.СодержаниеHTML) Тогда
		ФорматированныйДокумент	= Новый ФорматированныйДокумент;
		ФорматированныйДокумент.УстановитьHTML(СодержаниеПисьма.СодержаниеHTML, Новый Структура);
		ИсходноеТелоПисьма = ФорматированныйДокумент.ПолучитьТекст();		
	ИначеЕсли ЗначениеЗаполнено(СодержаниеПисьма.Содержание) Тогда
		ИсходноеТелоПисьма	= СодержаниеПисьма.Содержание;
	Иначе
		ТекстСообщения	= "Нет данных в содержимом письма.";
		Сообщить(ТекстСообщения);
		Возврат;	
	КонецЕсли;
	
	ИсходноеТелоПисьма	= СтрЗаменить(ИсходноеТелоПисьма, Символы.ВК, "");
	ИсходноеТелоПисьма	= СтрЗаменить(ИсходноеТелоПисьма, Символы.НПП, " ");
	
	ТекстПисьма	= ИсходноеТелоПисьма;
	
КонецПроцедуры

&НаСервере
Функция СодержаниеСобытия(Событие)
	
	Если ЗначениеЗаполнено(Событие.Содержание) ИЛИ ЗначениеЗаполнено(Событие.СодержаниеHTML) Тогда
		Возврат Новый Структура("Содержание, СодержаниеHTML", Событие.Содержание, Событие.СодержаниеHTML);	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
					|	ИдентификаторыСобытий.Идентификатор
					|ИЗ
					|	РегистрСведений.ИдентификаторыСобытий КАК ИдентификаторыСобытий
					|ГДЕ
					|	ИдентификаторыСобытий.Событие = &Событие";

	Запрос.УстановитьПараметр("Событие", Событие);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда		
		Возврат ЗагрузитьСодержаниеСообщенияИнтернетПочты(Выборка.Идентификатор, Перечисления.ВходящееИсходящееСобытие.Входящее, Событие.УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Новый Структура("Содержание, СодержаниеHTML", "", "");
					
КонецФункции

&НаСервере
Функция ЗагрузитьСодержаниеСообщенияИнтернетПочты(Идентификатор, ВходящееИсходящееСобытие, УчетнаяЗапись)
	
	Профиль = НовыйИнтернетПочтовыйПрофиль(УчетнаяЗапись);
	Соединение = Новый ИнтернетПочта;
	
	Соединение.Подключиться(Профиль, ПротоколИнтернетПочтыIMAP(УчетнаяЗапись));	
	ИнтернетПочтовыеСообщения = Соединение.Выбрать(Ложь, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Идентификатор), Ложь);
	
	ДанныеСобытия = Документы.Событие.НовыеДанныеСобытия();
	
	Для Каждого ТекИнтернетПочтовоеСообщение Из ИнтернетПочтовыеСообщения Цикл
		Для Каждого ТекИдентификатор Из ТекИнтернетПочтовоеСообщение.Идентификатор Цикл
			
			Документы.Событие.ЗаполнитьТекстыИзПочтовогоСообщения(ДанныеСобытия, ТекИнтернетПочтовоеСообщение);
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДанныеСобытия;
	
КонецФункции

&НаСервере
Функция НовыйИнтернетПочтовыйПрофиль(УчетнаяЗапись)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
						|	УчетныеЗаписиЭлектроннойПочты.СерверВходящейПочты КАК АдресСервераIMAP,
						|	УчетныеЗаписиЭлектроннойПочты.ПортСервераВходящейПочты КАК ПортIMAP,
						|	УчетныеЗаписиЭлектроннойПочты.ИспользоватьЗащищенноеСоединениеДляВходящейПочты КАК ИспользоватьSSLIMAP,
						|	УчетныеЗаписиЭлектроннойПочты.Пользователь КАК ПользовательIMAP,
						|	УчетныеЗаписиЭлектроннойПочты.ИспользоватьБезопасныйВходНаСерверВходящейПочты КАК ТолькоЗащищеннаяАутентификацияIMAP,
						|	УчетныеЗаписиЭлектроннойПочты.СерверИсходящейПочты КАК АдресСервераSMTP,
						|	УчетныеЗаписиЭлектроннойПочты.ПортСервераИсходящейПочты КАК ПортSMTP,
						|	УчетныеЗаписиЭлектроннойПочты.ИспользоватьЗащищенноеСоединениеДляИсходящейПочты КАК ИспользоватьSSLSMTP,
						|	УчетныеЗаписиЭлектроннойПочты.ПользовательSMTP КАК ПользовательSMTP,
						|	УчетныеЗаписиЭлектроннойПочты.ИспользоватьБезопасныйВходНаСерверИсходящейПочты КАК ТолькоЗащищеннаяАутентификацияSMTP,
						|	УчетныеЗаписиЭлектроннойПочты.ВремяОжидания КАК Таймаут
						|ИЗ
						|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
						|ГДЕ
						|	УчетныеЗаписиЭлектроннойПочты.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый ИнтернетПочтовыйПрофиль;
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	УстановитьПривилегированныйРежим(Истина);
	Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(УчетнаяЗапись, "Пароль,ПарольSMTP");
	УстановитьПривилегированныйРежим(Ложь);
	Результат.ПарольIMAP = Пароли.Пароль;
	Результат.ПарольSMTP = Пароли.ПарольSMTP;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПротоколИнтернетПочтыIMAP(УчетнаяЗапись)
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ПротоколВходящейПочты") = "IMAP" Тогда
		Возврат ПротоколИнтернетПочты.IMAP;
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Подключение к серверу входящей почты может быть выполнено только по протоколу IMAP.'");
	
КонецФункции

&НаКлиенте
Процедура Получить(Команда)
	ПолучитьНаСервере();
КонецПроцедуры
